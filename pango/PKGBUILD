pkgname=pango
pkgver=1.34.0
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('libthai' 'cairo' 'libxft' 'harfbuzz')
makedepends=('harfbuzz' 'fontconfig' 'cairo' 'libxft' 'libthai' 'gobject-introspection')
options=('!libtool')
install=pango.install
source=(http://download.gnome.org/sources/${pkgname}/${pkgver:0:4}/${pkgname}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('105da1efe019018a9de81681d88c9ace83d2111509ab625c4be9d124a1fdce27')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-silent-rules \
      --disable-debug \
      --disable-gtk-doc \
      --disable-gtk-doc-html \
      --with-included-modules=basic-fc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make -j1 DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/etc/pango"
}
