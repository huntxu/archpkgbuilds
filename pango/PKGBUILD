pkgname=pango
pkgver=1.29.4
pkgrel=1
pkgdesc="A library for layout and rendering of text"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('glib2' 'cairo' 'libxft' 'libthai' 'freetype2')
makedepends=('gobject-introspection' 'libxt')
options=('!libtool' '!emptydirs')
install=pango.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/1.29/${pkgname}-${pkgver}.tar.xz)
url="http://www.pango.org/"
sha256sums=('7ae8d1953e6098a2706df58c1f84555c06ace7006bb34c0e54ab9acd98c1127f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var \
      --enable-silent-rules \
      --disable-debug \
      --disable-gtk-doc \
      --disable-gtk-doc-html \
      --with-included-modules=basic-fc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
