pkgname=gnome-mplayer
pkgver=1.0.6
pkgrel=1
pkgdesc='A simple MPlayer GUI.'
arch=('i686' 'x86_64')
url='http://gnome-mplayer.googlecode.com/'
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'hicolor-icon-theme' 'dconf' 'gmtk')
install="${pkgname}.install"
source=("http://gnome-mplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('c1fc23faf70f3bbc23daf60f9b34832d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc \
              --enable-gtk3 \
              --disable-nautilus \
              --disable-gseal \
              --disable-schemas-compile \
              --disable-schemas-install \
              --with-dbus \
              --with-alsa \
              --with-libnotify \
              --without-libgpod \
              --without-libmusicbrainz3 \
              --without-gpm-old-method
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
