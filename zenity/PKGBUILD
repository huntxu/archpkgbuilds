pkgname=zenity
pkgver=3.2.0
pkgrel=1
pkgdesc="Display graphical dialog boxes from shell scripts"
arch=(i686 x86_64)
license=('LGPL')
depends=('gtk3' 'libnotify')
makedepends=('intltool' 'gtk-doc')
url="http://www.gnome.org"
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz)
sha256sums=('e6a778db3655957770653745a2ecaef08a41a71f4a0a92cb7c5dc8ce0e9902d8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --localstatedir=/var \
              --enable-silent-rules \
              --disable-scrollkeeper \
              --disable-maintainer-mode \
              --disable-nls \
              --disable-debug \
              --without-help-dir \
              --without-omf-dir
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/usr/share/locale
}
