pkgname=gimp-devel
pkgver=2.7.4
pkgrel=2
pkgdesc="GNU Image Manipulation Program (Development Version)"
arch=('i686' 'x86_64')
url="http://www.gimp.org/"
license=('GPL3' 'LGPL3')
depends=('lcms' 'libxpm' 'librsvg' 'dbus-glib' \
         'jasper' 'gegl' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('intltool' 'poppler-glib' 'alsa-lib' 'curl')
optdepends=('gutenprint: for sophisticated printing only as gimp has built-in cups print support'
            'libwebkit: for the help browser or web-page plug-ins'
            'poppler-glib: for pdf support'
            'alsa-lib: for MIDI event controller module'
            'curl: for URI support')
options=('!libtool' '!makeflags')
conflicts=('gimp')
provides=("gimp=${pkgver}")
install=gimp-devel.install
source=(ftp://ftp.gimp.org/pub/gimp/v${pkgver%.*}/gimp-${pkgver}.tar.bz2 linux.gpl)
md5sums=('bda95a29c3483b8ff458b06b1543f867'
         'bb27bc214261d36484093e857f015f38')

build() {
  cd "${srcdir}/gimp-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc \
      --disable-largefile \
      --disable-nls \
      --disable-glibtest \
      --disable-gtktest \
      --enable-mp \
      --disable-alsatest \
      --disable-python \
      --disable-gimp-console \
      --without-libmng \
      --without-libexif \
      --without-aa \
      --without-webkit \
      --without-gvfs \
      --without-wmf \
      --with-gif-compression=lzw
  make
}

package() {
  cd "${srcdir}/gimp-${pkgver}"
  make DESTDIR="${pkgdir}" install
  #sed -i 's|#![ ]*/usr/bin/env python|#!/usr/bin/env python2|' "${pkgdir}"/usr/lib/gimp/2.0/plug-ins/*.py
  install -D -m644 "${srcdir}/linux.gpl" "${pkgdir}/usr/share/gimp/2.0/palettes/Linux.gpl"

  ln -sf gimprc-${pkgver%.*}.5.gz "${pkgdir}/usr/share/man/man5/gimprc.5.gz"
  ln -sf gimptool-2.0.1.gz "${pkgdir}/usr/share/man/man1/gimptool.1.gz"
  ln -s gimp-${pkgver%.*}.1.gz "${pkgdir}/usr/share/man/man1/gimp.1.gz"

  ln -s gimp-${pkgver%.*} "${pkgdir}/usr/bin/gimp"
  ln -s gimptool-2.0 "${pkgdir}/usr/bin/gimptool"
}
