pkgname=atk
pkgver=2.8.0
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility"
arch=(i686 x86_64)
license=('LGPL')
depends=('glib2')
makedepends=('gobject-introspection')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
url='http://www.gtk.org/'
sha256sums=('b22519176226f3e07cf6d932b77852e6b6be4780977770704b32d0f4e0686df4')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --disable-glibtest \
              --disable-gtk-doc
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
