pkgname=xf86-video-nouveau
_gitdate=20120210
pkgver=0.0.16_git${_gitdate} # see configure.ac
pkgrel=1
pkgdesc="Open Source 3D acceleration driver for nVidia cards (experimental)"
arch=('i686' 'x86_64')
url="http://nouveau.freedesktop.org/wiki/"
license=('GPL') #and MIT, not yet a license file, see http://nouveau.freedesktop.org/wiki/FAQ#head-09f75d03eb30011c754038a3893119a70745de4e
depends=('libdrm' 'udev')
optdepends=('nouveau-dri:	experimental gallium3d features')
makedepends=('xorg-server-devel' 'libdrm' 'xf86driproto')
conflicts=('xorg-server<1.11.99.902')
options=('!libtool')
install=$pkgname.install
source=(ftp://ftp.archlinux.org/other/$pkgname/xf86-video-nouveau-${_gitdate}.tar.bz2)
md5sums=('f35c12d64542d0ea792a9e5af63a2e5a')

# source PKGBUILD && mksource
mksource() {
	mkdir /tmp/$pkgname-${_gitdate}
	pushd /tmp/$pkgname-${_gitdate}
	git clone -v --depth 1 git://anongit.freedesktop.org/nouveau/xf86-video-nouveau
	cd xf86-video-nouveau
	git archive --prefix=xf86-video-nouveau-${_gitdate}/ --format=tar HEAD | bzip2 > /tmp/$pkgname-${_gitdate}/$pkgname-${_gitdate}.tar.bz2
	popd
}

build() {
  cd $srcdir/xf86-video-nouveau-${_gitdate}
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $srcdir/xf86-video-nouveau-${_gitdate}
  make DESTDIR=$pkgdir install
}
