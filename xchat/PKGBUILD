pkgname=xchat
pkgver=2.8.8
pkgrel=7
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'openssl' 'dbus-glib' 'perl' 'libnotify')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin'
            'python2: for python plugin')
options=('!libtool')
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        xchat-2.8.8-libnotify07.patch
        xchat-2.8.8-link-against-libnotify.patch)
md5sums=('6775c44f38e84d06c06c336b32c4a452'
         '83359a807fa4f7faee90297576fb17a7'
         'dc30fd1203a93b19d47734ba55713b7a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/xchat-2.8.8-libnotify07.patch
  patch -Np1 -i ${srcdir}/xchat-2.8.8-link-against-libnotify.patch
  autoconf
  autoheader

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --disable-nls \
      --enable-threads=posix \
      --disable-socks \
      --disable-ipv6 \
      --enable-openssl \
      --disable-textfe \
      --disable-python \
      --enable-perl \
      --disable-perl_old \
      --disable-tcl \
      --enable-plugin \
      --enable-dbus \
      --enable-mmx \
      --enable-shm \
      --disable-spell \
      --disable-ntlm \
      --disable-glibtest \
      --disable-gtktest
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
