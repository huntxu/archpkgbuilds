pkgname=xchat
pkgver=2.8.8
pkgrel=12
pkgdesc="A GTK+ based IRC client"
arch=('i686' 'x86_64')
url="http://www.xchat.org/"
license=('GPL')
depends=('gtk2' 'openssl' 'dbus-glib' 'libnotify' 'perl')
optdepends=('enchant: for spell checking support' 'tcl: for tcl plugin'
            'python2: for python plugin')
options=('!libtool')
source=(http://www.xchat.org/files/source/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz
        xchat-2.8.8-libnotify07.patch
        xchat-2.8.8-link-against-libnotify.patch
        xchat-2.8.8-glib-2.31.patch)
sha1sums=('e12305da42d1aacc26c2ca25e239f393d4dd3532'
          'a053fba4e1911d1ee6a8248fe19e344797920fe3'
          '70c3cc29fc55ff35f701ef8ac23078b6e3761ce1'
          'aecaf6176a7cfd62555207b02f2793b360aa39da')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's/GDK_HAND1/GDK_HAND2/' src/fe-gtk/xtext.c
  patch -p1 -i "${srcdir}/xchat-2.8.8-libnotify07.patch"
  patch -p1 -i "${srcdir}/xchat-2.8.8-link-against-libnotify.patch"
  patch -p1 -i "${srcdir}/xchat-2.8.8-glib-2.31.patch"
  autoconf
  autoheader

  LIBS+="-lgmodule-2.0" ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --disable-nls \
      --enable-threads=posix \
      --disable-socks \
      --disable-ipv6 \
      --enable-openssl \
      --disable-textfe \
      --disable-python \
      --enable-perl \
      --disable-perl_old \
      --disable-tcl \
      --enable-plugin \
      --enable-dbus \
      --enable-mmx \
      --enable-shm \
      --disable-spell \
      --disable-ntlm \
      --disable-glibtest \
      --disable-gtktest
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
