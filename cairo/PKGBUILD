pkgname=cairo
pkgver=1.10.2
pkgrel=3
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"
depends=('libpng' 'libxrender' 'fontconfig' 'pixman' 'glib2' 'sh')
options=('!libtool')
source=(http://cairographics.org/releases/${pkgname}-${pkgver}.tar.gz
        cairo-1.10.0-buggy_gradients.patch)
sha1sums=('ccce5ae03f99c505db97c286a0c9a90a926d3c6e'
          '8b843a9934e5112b6188e5bcf4adfc1fdaf9fa04')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/cairo-1.10.0-buggy_gradients.patch"
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-xcb \
        --enable-xlib-xcb \
        --enable-tee
  make
}

#check() {
#  cd "${srcdir}/${pkgname}-${pkgver}"
#  make check || /bin/true
#}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
