pkgname=cairo
pkgver=1.12.14
pkgrel=3
pkgdesc="Cairo vector graphics library"
arch=(i686 x86_64)
license=('LGPL' 'MPL')
url="http://cairographics.org/"

makedepends=('mesa>=9.1' 'mesa-libgl>=9.1' 'librsvg' 'gtk2' 'poppler-glib' 'libspectre')
#optdepends=('xcb-util: for XCB backend') # really needed?
provides=('cairo-xcb')
replaces=('cairo-xcb')
options=('!libtool')
source=(http://cairographics.org/releases/$pkgname-$pkgver.tar.xz)
sha1sums=('9106ab09b2e7b9f90521b18dd4a7e9577eba6c15')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export CFLAGS=`echo $CFLAGS|sed -r 's/\-flto//'`
  ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --enable-tee \
        --enable-gl \
        --enable-egl \
        --enable-svg \
        --enable-ps \
        --enable-pdf \
        --enable-gobject \
        --enable-xcb \
        --enable-xlib-xcb
  make
}

package() {
  depends=('libpng' 'libxrender' 'libxext' 'fontconfig' 'pixman>=0.28.0' 'glib2' 'mesa>=9.1' 'libgl' 'sh') 

  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
