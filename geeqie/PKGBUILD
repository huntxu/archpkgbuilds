pkgname=geeqie
pkgver=1.1
pkgrel=2
pkgdesc="A lightweight image browser and viewer"
arch=('i686' 'x86_64')
url="http://geeqie.sourceforge.net/"
license=('GPL2')
depends=('gtk2' 'lcms' 'desktop-file-utils')
makedepends=('intltool')
optdepends=('librsvg: SVG rendering')
install=geeqie.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz 
        geeqie-fix-fullscreen.patch)
sha1sums=('77167479e91e03d9512535a146c5d2d77941257f'
          '57c8b058e6ec1fc2f691650b212acc5dafcd5891')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i"${srcdir}/geeqie-fix-fullscreen.patch"
  CPPFLAGS="-D_FILE_OFFSET_BITS=64" ./configure --prefix=/usr \
      --disable-doxygen-doc \
      --disable-doxygen-dot \
      --disable-largefile \
      --disable-nls \
      --disable-glibtest \
      --disable-gtktest \
      --disable-exiv2
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
