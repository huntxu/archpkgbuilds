pkgname=r
pkgver=2.14.1
pkgrel=4
pkgdesc="Language and environment for statistical computing and graphics"
arch=('i686' 'x86_64')
license=('GPL')
url=('http://www.r-project.org/')
depends=('blas' 'lapack' 'bzip2'  'libpng' 'libjpeg' 'libtiff'
         'ncurses' 'pcre' 'readline' 'zlib' 'perl' 'gcc-libs'
         'libxt' 'libxmu' 'pango' 'xz')
makedepends=('openjdk6' 'gcc-fortran')
options=('!makeflags')
source=("http://cran.r-project.org/src/base/R-2/R-${pkgver}.tar.gz")
sha1sums=('5803a213480a749e36bccc155aecb13070ade096')

build() {
   cd "${srcdir}/R-${pkgver}"
   sed -i 's|#define NeedFunctionPrototypes 0|#define NeedFunctionPrototypes 1|g' src/modules/X11/dataentry.c
   sed -i 's/_pcre_valid_utf8/_pcre_valid_utf/' src/main/util.c
   ./configure  --prefix=/usr \
		--libdir=/usr/lib \
                --sysconfdir=/etc/R \
		--datarootdir=/usr/share \
		  rsharedir=/usr/share/R/ \
		  rincludedir=/usr/include/R/ \
		  rdocdir=/usr/share/doc/R/ \
                --with-gnu-ld \
		--enable-R-shlib \
                --disable-largefile \
                --disable-nls \
                --with-blas \
                --with-lapack \
		--with-readline \
                --without-tcltk \
                --with-cairo \
                --with-libpng \
                --with-jpeglib \
                --with-system-zlib \
                --with-system-bzlib  \
                --with-system-pcre \
                --with-system-xz \
                --with-x \
                F77=gfortran \
		LIBnn=lib
   make
}

package() {
   cd "${srcdir}/R-${pkgver}"
   make -j1 DESTDIR="${pkgdir}" install

   #  Fixup R wrapper scripts.
   sed -i "s|${pkgdir} ||" "${pkgdir}/usr/bin/R"
   rm "${pkgdir}/usr/lib/R/bin/R"
   cd "${pkgdir}/usr/lib/R/bin"
   ln -s ../../../bin/R

  # move the config directory to /etc and create symlinks
  install -d "${pkgdir}/etc/R"
  cd "${pkgdir}/usr/lib/R/etc"
  for i in *; do
    mv -f ${i} "${pkgdir}/etc/R"
    ln -s /etc/R/${i} ${i}
  done
}
