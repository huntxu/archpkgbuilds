pkgname=awesome
pkgver=3.5.1
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'gdk-pixbuf2'
  'imlib2'
  'libxcursor'
  'libxdg-basedir'
  'lua'
  'lua-lgi'
  'pango'
  'startup-notification'
  'xcb-util-image'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xorg-xmessage'
)
makedepends=(
  'cmake'
  'imagemagick'
)
provides=('notification-daemon')
source=("$url/download/$pkgname-$pkgver.tar.xz")
md5sums=('f528f66ddcdb07f24e6f494837371702')

build() {
  cd $pkgname-$pkgver
  cmake \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DGENERATE_MANPAGES=OFF \
    -DGENERATE_LUADOC=OFF \
    -DLUA_LIBRARY=/usr/lib/liblua.so.5.2
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
