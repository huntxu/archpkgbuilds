pkgname=awesome
pkgver=3.4.12
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'imlib2'
  'libev'
  'libxdg-basedir'
  'lua'
  'pango'
  'startup-notification'
  'xcb-util-image'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xorg-xmessage'
)
makedepends=(
  'cmake'
  'gperf'
  'imagemagick'
)
provides=('notification-daemon')
source=("$url/download/$pkgname-$pkgver.tar.xz"
        "awesome-xcursor.patch")
md5sums=('2c3490b820a19c044290027e6f2eb6c8'
         '9132ff6842484e1a46eb37f4e23cff1c')

build() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../awesome-xcursor.patch

  make CMAKE_ARGS=" -DPREFIX=/usr -DSYSCONFDIR=/etc \
	-DCMAKE_BUILD_TYPE=RELEASE -DGENERATE_MANPAGES=OFF -DGENERATE_LUADOC=OFF"
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
