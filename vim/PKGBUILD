pkgname=vim
_topver=7.3
_patchlevel=487
pkgver=${_topver}.${_patchlevel}
pkgrel=1
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor'
arch=('x86_64')
license=('custom:vim')
url="http://www.vim.org"
depends=('perl' 'libxt' 'gtk2')
makedepends=('sed' 'gettext' 'rsync' 'mercurial' 'pkgconfig')
backup=(etc/vimrc)
source=(vimrc archlinux.vim)
md5sums=('e57777374891063b9ca48a1fe392ac05' '10353a61aadc3f276692d0e17db1478e')

__hgroot='http://vim.googlecode.com/hg/'
__hgrepo='vim'
__hgbranch='default'
_versiondir="vim${_topver//./}"

_update_source() {
    cd ${srcdir}
    msg2 'Checking out source from Mercurial...'

    ln -sf ../${__hgrepo} ./${__hgrepo}
    cd ${__hgrepo}
    hg pull -b ${__hgbranch} || warning 'hg pull failed!'
    hg update
    cd ..
    unlink ${__hgrepo}
}

build()
{
    _update_source

    cd ${srcdir}
    rsync -a --exclude='.hg/' ../${__hgrepo}/ vim-build

    msg2 'Patching...'
    # define the place for the global (g)vimrc file (set to /etc/vimrc)
    sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
    sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    vim-build/src/feature.h
    # fix python name
    sed -i -e 's|vi_cv_path_python, python|vi_cv_path_python, python2|' \
    vim-build/src/configure.in
    (cd vim-build/src && autoconf)

    msg2 'Building...'
    cd ${srcdir}/vim-build

    ./configure --prefix=/usr \
                --localstatedir=/var/lib/vim \
                --mandir=/usr/share/man \
                --with-compiledby=huntxu \
                --with-features=big \
                --with-x=yes \
                --disable-darwin \
                --disable-selinux \
                --disable-xsmp \
                --disable-xsmp-interact \
                --disable-mzschemeinterp \
                --enable-perlinterp \
                --disable-tclinterp \
                --enable-cscope \
                --disable-workshop \
                --disable-netbeans \
                --disable-sniff \
                --enable-multibyte \
                --disable-hangulinput \
                --enable-xim \
                --disable-fontset \
                --enable-gui=gtk2 \
                --enable-gtk2-check \
                --disable-gnome-check \
                --disable-motif-check \
                --disable-athena-check \
                --disable-nextaw-check \
                --disable-carbon-check \
                --disable-gtktest \
                --disable-acl \
                --disable-gpm \
                --disable-sysmouse \
                --disable-nls
    make
}

package() {
    cd ${srcdir}/vim-build
    make -j1 VIMRCLOC=/etc DESTDIR=${pkgdir} install

    # provided by (n)vi in core
    rm ${pkgdir}/usr/bin/{ex,view}

    # delete some manpages
    find ${pkgdir}/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
        cd ${_mandir}
        rm -f ex.1 view.1 # provided by (n)vi
    done

    # Don't forget logtalk.dict
    install -Dm644 ${srcdir}/vim-build/runtime/ftplugin/logtalk.dict \
        ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/logtalk.dict || return 1

    # fix FS#17216
    sed -i 's|messages,/var|messages,/var/log/messages.log,/var|' \
        ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim

    # patch filetype.vim for better handling of pacman related files
    sed -i "s/rpmsave/pacsave/;s/rpmnew/pacnew/;s/,\*\.ebuild/\0,PKGBUILD*,*.install/" \
        ${pkgdir}/usr/share/vim/${_versiondir}/filetype.vim
    sed -i "/find the end/,+3{s/changelog_date_entry_search/changelog_date_end_entry_search/}" \
        ${pkgdir}/usr/share/vim/${_versiondir}/ftplugin/changelog.vim

    # rc files
    install -Dm644 ${srcdir}/vimrc ${pkgdir}/etc/vimrc
    install -Dm644 ${srcdir}/archlinux.vim \
        ${pkgdir}/usr/share/vim/vimfiles/archlinux.vim
    # rgb.txt file
    install -Dm644 ${srcdir}/vim-build/runtime/rgb.txt \
        ${pkgdir}/usr/share/vim/${_versiondir}/rgb.txt
}
