pkgname=evince
pkgver=3.8.0
pkgrel=1
pkgdesc="Simply a document viewer"
url="http://projects.gnome.org/evince/"
arch=(i686 x86_64)
license=(GPL)
depends=(gtk3 libgxps libspectre gsfonts poppler-glib djvulibre gnome-icon-theme desktop-file-utils dconf)
makedepends=(gobject-introspection intltool itstool)
install=evince.install
options=('!libtool' '!emptydirs')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('cf136d2a025ef486c4e248e0dc134e7a9420557d9edc0df1ceb6a6330fbd4e9a')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --libexecdir=/usr/lib/evince \
      --disable-static \
      --disable-nls \
      --disable-schemas-compile \
      --disable-tests \
      --disable-nautilus \
      --enable-viewer \
      --disable-thumbnailer \
      --disable-previewer \
      --enable-introspection \
      --enable-pdf \
      --enable-ps \
      --enable-tiff \
      --enable-djvu \
      --disable-dvi \
      --enable-comics \
      --enable-xps \
      --disable-gtk-doc-html \
      --without-keyring
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
