pkgname=glib2
pkgver=2.32.3
pkgrel=1
pkgdesc="Common C routines used by GTK+ and other libs"
url="http://www.gtk.org/"
arch=(i686 x86_64)
license=('LGPL')
depends=('pcre' 'libffi')
makedepends=('pkg-config' 'python2')
optdepends=('python2: for gdbus-codegen')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz
        glib2.sh
        glib2.csh
        revert-warn-glib-compile-schemas.patch)
sha256sums=('b65ceb462807e4a2f91c95e4293ce6bbefca308cb44a1407bcfdd9e40363ff4d'
            '9456872cdedcc639fb679448d74b85b0facf81033e27157d2861b991823b5a2a'
            '8d5626ffa361304ad3696493c0ef041d0ab10c857f6ef32116b3e2878ecf89e3'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97')

build() {
  cd glib-$pkgver
  patch -Rp1 -i "$srcdir/revert-warn-glib-compile-schemas.patch"
  export CFLAGS=`echo $CFLAGS|sed -r 's/\-flto//'`
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --enable-silent-rules \
      --disable-maintainer-mode \
      --disable-largefile \
      --disable-selinux \
      --disable-fam \
      --disable-xattr \
      --disable-gtk-doc \
      --disable-gtk-doc-html \
      --with-pcre=system
  make
}

package() {
  cd glib-$pkgver
  make completiondir=/usr/share/bash-completion/completions DESTDIR="$pkgdir" install

  install -d "$pkgdir/etc/profile.d"
  install -m755 "$srcdir/glib2.sh" "$pkgdir/etc/profile.d/"
  install -m755 "$srcdir/glib2.csh" "$pkgdir/etc/profile.d/"

  for _i in "$pkgdir/usr/share/bash-completion/completions/"*; do
      chmod -x "$_i"
  done
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir"/usr/bin/gdbus-codegen
}
