pkgname=('pidgin' 'libpurple')
pkgver=2.10.7
pkgrel=2
arch=('i686' 'x86_64')
url="http://pidgin.im/"
license=('GPL')
makedepends=('startup-notification' 'libxss' 'nss' 'libsasl' 'gnutls'
             'dbus-glib' 'python2' 'hicolor-icon-theme'
             'ca-certificates' 'intltool')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2)
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        pidgin-2.10.7-link-libirc-to-libsasl2.patch)
sha256sums=('eba32994eca20d1cf24a4261b059b2de71a1ec2dd0926e904074b0db49f7f192'
            '063723d5dc5726c43137b4b383c9d07c2c008391f6a626faaf6cedd31a2f1e8f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://developer.pidgin.im/ticket/15517
  patch -Np1 -i "$srcdir/pidgin-2.10.7-link-libirc-to-libsasl2.patch"
  autoreconf -vi

  # Use Python 2
  sed -i 's/env python$/&2/' */plugins/*.py \
    libpurple/purple-{remote,notifications-example,url-handler}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-largefile \
    --disable-nls \
    --disable-nls \
    --enable-gtkui \
    --disable-consoleui \
    --enable-screensaver \
    --disable-sm \
    --enable-startup-notification \
    --disable-gtkspell \
    --disable-gevolution \
    --enable-cap \
    --disable-gestures \
    --disable-schemas-install \
    --disable-gstreamer \
    --disable-gstreamer-interfaces \
    --disable-farsight \
    --disable-vv \
    --disable-idn \
    --disable-meanwhile \
    --disable-avahi \
    --enable-plugins \
    --disable-fortify \
    --enable-dbus \
    --disable-nm \
    --disable-mono \
    --enable-perl \
    --enable-gnutls \
    --enable-nss \
    --disable-tcl \
    --disable-tk \
    --enable-cyrus-sasl \
    --disable-doxygen \
    --disable-dot \
    --disable-devhelp \
    --disable-debug \
    --with-dynamic-prpls="jabber" \
    --with-system-ssl-certs=/etc/ssl/certs \
    --with-python=/usr/bin/python2
  make
}

package_pidgin(){
  pkgdesc="Multi-protocol instant messaging client"
  depends=('libpurple' 'startup-notification' 'libxss'
           'hicolor-icon-theme')
  install=pidgin.install

  cd "$srcdir/pidgin-$pkgver"

  # For linking
  make -C libpurple DESTDIR="$pkgdir" install-libLTLIBRARIES

  make -C pidgin DESTDIR="$pkgdir" install
  make -C doc DESTDIR="$pkgdir" install

  # Remove files that are packaged in libpurle
  make -C libpurple DESTDIR="$pkgdir" uninstall-libLTLIBRARIES
}

package_libpurple(){
  pkgdesc="IM library extracted from Pidgin"
  depends=('gnutls' 'libsasl' 'dbus-glib' 'nss')
  optdepends=('ca-certificates: SSL CA certificates' 'python2-dbus: for purple-remote and purple-url-handler')

  cd "$srcdir/pidgin-$pkgver"

  for _dir in libpurple share/sounds share/ca-certs m4macros; do
    make -C "$_dir" DESTDIR="$pkgdir" install
  done
}
