pkgname=libdrm
pkgver=2.4.32
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services"
arch=(i686 x86_64)
license=('custom')
depends=('glibc' 'libpciaccess')
makedepends=('cairo')
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
        COPYING)
sha1sums=('4aab6cc1c0d2a4822ffd83bd3da0e7a5479bf842'
          '825ff5e0c4238b31bdea52f104bfec8949270e25'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"

  autoreconf --force --install
  ./configure --prefix=/usr \
      --enable-udev \
      --enable-libkms \
      --enable-intel \
      --disable-radeon
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/"
}
